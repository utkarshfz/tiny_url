buildscript {
	dependencies {
		classpath("org.postgresql:postgresql:42.7.3")
		classpath("org.flywaydb:flyway-database-postgresql:10.13.0")
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.flywaydb.flyway' version '10.13.0'
	id 'jacoco'
}

group = 'com.demo'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.11"
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"**/model/*",
					"**/entity/*",
					"**/exception/*",
					"**/constants/*",
					"**/configuration/*",
					"**/repository/*"
			])
		}))
	}

	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacoco')
	}
}

flyway {
	url = 'jdbc:postgresql://localhost:5433/tiny_url'
	user = System.getenv('DB_USER_NAME')
	password = System.getenv('DB_PASSWORD')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.21'
	implementation 'javax.validation:validation-api:2.0.1.Final'
	implementation 'org.postgresql:postgresql:42.7.3'
	implementation 'com.101tec:zkclient:0.10'
	implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
	implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
	implementation 'org.springframework.data:spring-data-redis:3.3.0'
	implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.1.0'
	implementation 'org.slf4j:slf4j-api:2.0.13'
	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor "org.projectlombok:lombok:1.18.30"
	testAnnotationProcessor "org.projectlombok:lombok:1.18.30"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
